if(CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)
  set(BUILD_TESTS_DEFAULT ON)
else()
  set(BUILD_TESTS_DEFAULT OFF)
endif()

option(BUILD_TESTS "Build sources in `/test` directory" "${BUILD_TESTS_DEFAULT}")
if(NOT BUILD_TESTS)
  return()
endif()

function(build_test test_name test_source)
  message(STATUS "${PROJECT_NAME}: adding test: \"${test_name}\"")
  add_executable(${test_name} ${test_source})
  target_link_libraries(${test_name} PRIVATE ${PROJECT_NAME})
  target_include_directories(${test_name} PRIVATE ${${PROJECT_NAME}_INCLUDES})
  target_compile_features(${test_name} PUBLIC cxx_std_17)
  set_target_properties(${test_name} PROPERTIES CXX_EXTENSIONS OFF)
endfunction()

file(GLOB test_sources LIST_DIRECTORIES false ${CMAKE_CURRENT_SOURCE_DIR}/*.cpp)
foreach(test_source ${test_sources})
  get_filename_component(test_name ${test_source} NAME_WE)
  build_test(${test_name} ${test_source})
endforeach()
